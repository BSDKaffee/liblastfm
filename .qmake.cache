win32: ROOT_DIR = $$system( cd )
else:  ROOT_DIR = $$system( pwd )

BUILD_DIR = _build
DESTDIR = $$ROOT_DIR/_bin
LIBS += -L$$DESTDIR

OBJECTS_DIR = $$BUILD_DIR
MOC_DIR = $$BUILD_DIR
UI_DIR = $$BUILD_DIR
RCC_DIR = $$BUILD_DIR
INCLUDEPATH += $$ROOT_DIR $$ROOT_DIR/_include

win32:DEFINES += _CRT_SECURE_NO_WARNINGS WIN32_LEAN_AND_MEAN LASTFM_OHAI_QMAKE

# allow use of and, or, etc. as symbols
unix:QMAKE_CXXFLAGS += -fno-operator-names

macx* {
	QMAKE_PKGINFO_TYPEINFO = last
	QMAKE_MACOSX_DEPLOYMENT_TARGET = 10.4
}

CONFIG( debug, debug|release ):CONFIG -= release

linux*:CONFIG += link_pkgconfig

target.path = $$INSTALL_DIR/lib

unix{
    QMAKE_CXXFLAGS_RELEASE -= -O2 -Os
    QMAKE_CXXFLAGS_RELEASE += -fvisibility-inlines-hidden -fvisibility=hidden -O3 -fomit-frame-pointer
    linux*{
        QMAKE_CXXFLAGS_RELEASE += -freorder-blocks -fno-reorder-functions -w
    }    
    release:QMAKE_POST_LINK=strip $(DESTDIR)/$(TARGET)
}
