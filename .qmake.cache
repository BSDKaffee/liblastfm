win32:ROOT_DIR = $$system( cd )
else:ROOT_DIR = $$system( pwd )

BUILD_DIR = _build
DESTDIR = $$ROOT_DIR/_bin
LIBS += -L$$DESTDIR

OBJECTS_DIR = $$BUILD_DIR
MOC_DIR = $$BUILD_DIR
UI_DIR = $$BUILD_DIR
RCC_DIR = $$BUILD_DIR
INCLUDEPATH += $$ROOT_DIR/_include

win32:DEFINES += _CRT_SECURE_NO_WARNINGS WIN32_LEAN_AND_MEAN LASTFM_OHAI_QMAKE

macx* {
	QMAKE_PKGINFO_TYPEINFO = last
	QMAKE_MACOSX_DEPLOYMENT_TARGET = 10.4
}

CONFIG( debug, debug|release ):CONFIG -= release

target.path = $$INSTALL_DIR/lib

unix {
    # allow use of 'and', 'or', etc. as symbols
    QMAKE_CXXFLAGS += -fno-operator-names
	QMAKE_CXXFLAGS_RELEASE += -fvisibility-inlines-hidden -fvisibility=hidden
    # I don't want your emails telling me to fix the warnings :P
    linux:QMAKE_CXXFLAGS_RELEASE += -w
    # OHAI!
    CONFIG(mxcl) {
        QMAKE_CXXFLAGS_RELEASE -= -O2 -Os
        QMAKE_CXXFLAGS_RELEASE += -O3 -fomit-frame-pointer
        QMAKE_POST_LINK=strip $(DESTDIR)/$(TARGET)
    }
}
