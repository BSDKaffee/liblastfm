ROOT_DIR = $$system( cd )

BUILD_DIR = _build
DESTDIR = $$ROOT_DIR/_bin
LIBS += -L$$DESTDIR

OBJECTS_DIR = $$BUILD_DIR
MOC_DIR = $$BUILD_DIR
UI_DIR = $$BUILD_DIR
RCC_DIR = $$BUILD_DIR
INCLUDEPATH += $$ROOT_DIR/_include

win32:DEFINES += _CRT_SECURE_NO_WARNINGS WIN32_LEAN_AND_MEAN LASTFM_OHAI_QMAKE

# allow use of and, or, etc. as symbols
unix:QMAKE_CXXFLAGS += -fno-operator-names

macx* {
	QMAKE_PKGINFO_TYPEINFO = last
	QMAKE_MACOSX_DEPLOYMENT_TARGET = 10.4
}

CONFIG( debug, debug|release ):CONFIG -= release

target.path = $$INSTALL_DIR/lib

CONFIG(mxcl){
    QMAKE_CXXFLAGS_RELEASE -= -O2 -Os
    QMAKE_CXXFLAGS_RELEASE += -fvisibility-inlines-hidden -fvisibility=hidden -O3 -fomit-frame-pointer
    linux:QMAKE_CXXFLAGS_RELEASE += -freorder-blocks -fno-reorder-functions
    release:QMAKE_POST_LINK=strip $(DESTDIR)/$(TARGET)
}

linux{
    CONFIG += link_pkgconfig
    # I don't want your emails telling me to fix the warnings :P
    QMAKE_CXXFLAGS_RELEASE += -w
}
